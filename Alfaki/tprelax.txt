$ontext
    File name: tprelax.gms.
    Author: Mohammed Alfaki, December, 2010.
    GAMS model for the TP-relaxation.
$offtext

#===============================================================================
# Declare options
#===============================================================================
options optcr=1.e-9, limrow=0, limcol=0, iterlim=1.e9, reslim=1.e9;

#===============================================================================
# Declare sets
#===============================================================================
     set l(i);
l(i) = i(i)-s(i)-t(i);

#===============================================================================
# Declare variables/bounds
#===============================================================================
variable cost;
positive variables y(i,t), f(s,i), x(s,i,t);

y.up(i,t) = a(i,t);
x.up(s,i,t) = min(bu(s),bu(i),bu(t))*a(s,i)*a(i,t);
f.up(s,i) = min(bu(s),bu(i))*a(s,i);

#===============================================================================
# Declare constraints
#===============================================================================
equations obj, sflowcaplb(s), sflowcapub(s), pflowcap(i), tflowcaplb(t), 
          tflowcapub(t), qualub(t,k), propblnc(i), rlt1(s,j), rlt2(i,t), 
          vexlb(s,i,t), vexub(s,i,t), cavlb(s,i,t), cavub(s,i,t);

#===============================================================================
# Define constraints
#===============================================================================
#-----------------------------Objective function--------------------------------
obj.. cost =e= sum((s,l,t)$(a(s,l)*a(l,t)>0),(c(s,l) + c(l,t))*x(s,l,t)) +
                                     sum((s,t)$(a(s,t)>0), c(s,t)*f(s,t));
#-------------------------Raw material availabilities---------------------------
sflowcaplb(s)$(bl(s)>0).. sum((l,t)$(a(s,l)*a(l,t)>0), x(s,l,t)) + 
                              sum(t$(a(s,t)>0), f(s,t)) =g= bl(s);
sflowcapub(s)$(bu(s)<+inf).. sum((l,t)$(a(s,l)*a(l,t)>0), x(s,l,t)) +
                                 sum(t$(a(s,t)>0), f(s,t)) =l= bu(s);
#-------------------------------Pool capacities---------------------------------
pflowcap(l)$(bu(l)<+inf).. sum((s,t)$(a(s,l)*a(l,t)>0), x(s,l,t)) =l= bu(l);
#-------------------------Product demand restrictions---------------------------
tflowcaplb(t)$(bl(t)>0).. sum((s,l)$(a(s,l)*a(l,t)>0), x(s,l,t)) + 
                              sum(s$(a(s,t)>0), f(s,t)) =g= bl(t);
tflowcapub(t)$(bu(t)<+inf).. sum((s,l)$(a(s,l)*a(l,t)>0), x(s,l,t)) + 
                                 sum(s$(a(s,t)>0), f(s,t)) =l= bu(t);
#------------------------Product quality specifications-------------------------
qualub(t,k)$(abs(q(t,k))>0 and abs(q(t,k))<+inf).. 
                  sum((l,s)$(a(s,l)*a(l,t)>0), (q(s,k) - q(t,k))*x(s,l,t)) + 
                           sum(s$(a(s,t)>0), (q(s,k) - q(t,k))*f(s,t)) =l= 0;
#------------------------------Proportion balances------------------------------
propblnc(l).. sum(t$(a(l,t)>0), y(l,t)) =e= 1;
#--------------------------RLT for proportion balances--------------------------
rlt1(s,l)$(a(s,l)>0).. sum(t$(a(l,t)>0), x(s,l,t)) =e= f(s,l);
#----------------------------RLT for pool capacities----------------------------
rlt2(l,t)$(a(l,t)>0 and bu(l)<+inf).. sum(s$(a(s,l)>0), x(s,l,t)) =l= 
                                                        bu(l)*y(l,t);
#---------------Convex/concave envelopes of x(s,l,t) = y(l,t)*f(s,l)------------
vexlb(s,l,t)$(a(s,l)*a(l,t)>0).. x(s,l,t) =g= y.lo(l,t)*f(s,l) +
                                        y(l,t)*f.lo(s,l) - y.lo(l,t)*f.lo(s,l); 
vexub(s,l,t)$(a(s,l)*a(l,t)>0).. x(s,l,t) =g= y.up(l,t)*f(s,l) +
                                        y(l,t)*f.up(s,l) - y.up(l,t)*f.up(s,l);
cavlb(s,l,t)$(a(s,l)*a(l,t)>0).. x(s,l,t) =l= y.lo(l,t)*f(s,l) +
                                        y(l,t)*f.up(s,l) - y.lo(l,t)*f.up(s,l);
cavub(s,l,t)$(a(s,l)*a(l,t)>0).. x(s,l,t) =l= y.up(l,t)*f(s,l) +
                                        y(l,t)*f.lo(s,l) - y.up(l,t)*f.lo(s,l);

#===============================================================================
# Solve the model
#===============================================================================
option lp = cplex;
model tprelax /all/;
tprelax.solprint = 2;
solve tprelax minimizing cost using lp;
#============================Print solution information=========================
scalars ae, re;
ae = abs(tprelax.objest - tprelax.objval);
re = ae/abs(tprelax.objest);
file line;
put_utility line 'msg' / '#####' ' Solution Summary #####';
line.nd = 6;
line.nw = 18;
put_utility line 'msg' / tprelax.objval tprelax.objest tprelax.resusd re ae;
line.nd = 0;
put_utility line 'msg' / tprelax.solvestat tprelax.iterusd;
option f:6:0:2;display f.l;
option y:6:0:2;display y.l;
#========================================END====================================